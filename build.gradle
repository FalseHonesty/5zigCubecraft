import org.gradle.internal.os.OperatingSystem;

apply plugin: 'java'

version = "1.0.0"

task wrapper(type: Wrapper) {
    gradleVersion = '2.14'
}

jar {
    from {
        configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

repositories {
    jcenter()
}

def minecraftFolder;
if (OperatingSystem.current().isMacOsX()) {
    minecraftFolder = "${System.properties['user.home']}/Library/Application Support/minecraft";
} else if (OperatingSystem.current().isLinux()) {
    minecraftFolder = "${System.properties['user.home']}/.minecraft";
} else if (OperatingSystem.current().isWindows()) {
    minecraftFolder = "$System.env.APPDATA/.minecraft";
} else {
    throw new FileNotFoundException("No minecraft folder detected");
}

def minecraftLibraries = fileTree(dir: "$minecraftFolder/libraries", include: '**/*.jar')

dependencies {
    compile 'org.slf4j:slf4j-api:1.7.13'
    compile 'org.json:json:20160810'
    compile 'org.iq80.leveldb:leveldb:0.9'

    compileOnly 'org.lwjgl.lwjgl:lwjgl:2.9.1'
    compileOnly fileTree(dir: 'libs', include: 'The-5zig-API-*.jar')
    compileOnly minecraftLibraries.matching { include '**/authlib-1.5.22.jar' }

    testCompile 'junit:junit:4.12'
}
